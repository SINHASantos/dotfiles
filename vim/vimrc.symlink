"
"   /\\\        /\\\
"   \/\\\       \/\\\
"    \//\\\      /\\\   /\\\
"      \//\\\    /\\\   \///     /\\\\\  /\\\\\    /\\/\\\\\\\     /\\\\\\\\
"        \//\\\  /\\\     /\\\  /\\\///\\\\\///\\\ \/\\\/////\\\  /\\\//////
"          \//\\\/\\\     \/\\\ \/\\\ \//\\\  \/\\\ \/\\\   \///  /\\\
"            \//\\\\\      \/\\\ \/\\\  \/\\\  \/\\\ \/\\\        \//\\\
"              \//\\\       \/\\\ \/\\\  \/\\\  \/\\\ \/\\\         \///\\\\\\\\
"                \///        \///  \///   \///   \///  \///            \////////
"
" Welcome to my vimrc. If you have not already I'd recommend that you run
" `~/dotfiles/script/bootstrap` to get everything setup correctly.
"

" ========================================
" == Mandatory setup =====================
" ========================================

scriptencoding utf-8
set encoding=utf-8

set nocompatible
filetype off

set rtp+=~/.vim/bundle/vundle/
call vundle#rc()

" ========================================
" == Plugins =============================
" ========================================

" core
Plugin 'gmarik/vundle'
Plugin 'christoomey/vim-tmux-navigator'
Plugin 'wincent/command-t'

" misc
Plugin 'AndrewRadev/switch.vim'
Plugin 'Raimondi/delimitMate'
Plugin 'SirVer/ultisnips'
Plugin 'christoomey/vim-sort-motion'
Plugin 'davidpdrsn/vim-smart-commandt'
Plugin 'davidpdrsn/vim-spectacular'
Plugin 'ervandew/supertab'
Plugin 'godlygeek/tabular'
Plugin 'jgdavey/tslime.vim'
Plugin 'kana/vim-textobj-user'
Plugin 'mattn/emmet-vim'
Plugin 'rking/ag.vim'
Plugin 'sjl/gundo.vim'
Plugin 'terryma/vim-multiple-cursors'
Plugin 'tpope/vim-abolish'
Plugin 'tpope/vim-commentary'
Plugin 'tpope/vim-dispatch'
Plugin 'tpope/vim-endwise'
Plugin 'tpope/vim-fugitive'
Plugin 'tpope/vim-repeat'
Plugin 'tpope/vim-surround'
Plugin 'tpope/vim-unimpaired'
Plugin 'tpope/vim-vinegar'

" syntax
Plugin 'kchmck/vim-coffee-script'
Plugin 'othree/html5-syntax.vim'
Plugin 'travitch/hasksyn'
Plugin 'vim-scripts/vim-json-bundle'
Plugin 'digitaltoad/vim-jade'

" languages: ruby
Plugin 'nelstrom/vim-textobj-rubyblock'
Plugin 'tpope/vim-rails'
Plugin 'tpope/vim-rbenv'
Plugin 'vim-ruby/vim-ruby'

" languages: php
Plugin 'StanAngeloff/php.vim'

" colors
Plugin 'altercation/vim-colors-solarized'
Plugin 'nanotech/jellybeans.vim'

" experimental
Plugin 'plasticboy/vim-markdown'
Plugin 'pbrisbin/vim-mkdir'
Plugin 'croaky/vim-colors-github'
Plugin 'thoughtbot/vim-rspec'

" Enable built-in matchit plugin
runtime macros/matchit.vim

" ========================================
" == More mandatory setup ================
" ========================================

filetype plugin indent on
syntax enable

" ========================================
" == General config ======================
" ========================================

" misc
color jellybeans
set fillchars+=vert:\             " Don't show pipes in vertical splits
set grepprg=ag\ --nogroup\ --nocolor\ -i
set backspace=indent,eol,start    " Backspace over everything in insert mode
set hidden                        " Don't unload buffers when leaving them
set nospell                       " Disable spell checking
set spelllang=en_us               " Use english US for spell checking
set scrolljump=5                  " Scroll more than one line
set scrolloff=3                   " Min. lines to keep above or below the cursor when scrolling
set shell=/bin/zsh                " Use zsh as shell
set splitbelow                    " Open splits below
set splitright                    " Open splits to the right
set tags=./tags,tags;$HOME        " Tell Vim where to look for tags files
set timeout                       " Lower the delay of escaping out of other modes
set visualbell                    " Disable annoying beep
set wildmenu                      " Enable command-line like completion
set wildmode=list:longest         " List all matches and complete till longest common string
set wrap                          " Wrap long lines
set noesckeys
set ttimeout                      " Set behavior of when partial mappings are pressed
set ttimeoutlen=1
set nojoinspaces                  " Insert only one space when joining lines that contain sentence-terminating punctuation like `.`.

" ui
set t_Co=256                      " More colors please
if has("gui_running")
  set background=dark             " Tell Vim the color of my background
else
  set background=light            " Tell Vim the color of my background
endif
set laststatus=2                  " Always show the status line
set linebreak                     " Don't break lines in the middle of words
set list                          " Show unprintable characters
set listchars=tab:▸\              " Char representing a tab
set listchars+=extends:❯          " Char representing an extending line
set listchars+=nbsp:␣             " Non breaking space
set listchars+=precedes:❮         " Char representing an extending line in the other direction
set listchars+=trail:·            " Show trailing spaces as dots
set nocursorcolumn                " Don't highlight the current column
set nocursorline                  " Don't highlight the current line
set number                        " Don't show line numbers
set numberwidth=4                 " The width of the number column
set relativenumber                " Show relative numbers
" set showbreak=↪                   " Show wraped lines with this char
set guifont=Source\ Code\ Pro:h14 " Set gui font
set colorcolumn=80                " Show a line at column 80 so we know when we've passed it

" searching
set hlsearch                      " Highlight search matches
set ignorecase                    " Do case insensitive search unless there are capital letters
set incsearch                     " Perform incremental searching

" backups & undo
set backup
set backupdir=~/.vim/tmp/backup/
set backupskip=/tmp/*,/private/tmp/*
set noswapfile
set history=1000                  " Sets how many lines of history vim has to remember
set undodir=~/.vim/tmp/undo/
set undofile
set undolevels=1000               " How many steps of undo history vim should remember
set writebackup

" indentation
set expandtab                     " Indent with spaces
set shiftwidth=2                  " Number of spaces to use when indenting
set smartindent                   " Auto indent new lines
set softtabstop=2                 " Number of spaces a <tab> counts for when inserting
set tabstop=2                     " Number of spaces a <tab> counts for

" folds
set foldenable                    " Enable folds
set foldlevelstart=99             " Fold by indentation
set foldmethod=indent             " Fold by indentation

" the status line
set statusline=%f       "tail of the filename
set statusline+=\ %h      "help file flag
set statusline+=%m      "modified flag
set statusline+=%r      "read only flag
set statusline+=%y      "filetype
set statusline+=%=      "left/right separator
set statusline+=%c,     "cursor column
set statusline+=%l/%L   "cursor line/total lines
set statusline+=\ %P    "percent through file
set statusline+=\ %{fugitive#statusline()}

" ========================================
" == Auto commands =======================
" ========================================

augroup highlightingLongLines
  autocmd!
  autocmd FileType *        match ErrorMsg '\%>80v.\+'
  autocmd FileType calendar match none
  autocmd FileType conf     match none
  autocmd FileType css      match none
  autocmd FileType eruby    match none
  autocmd FileType help     match none
  autocmd FileType html     match none
  autocmd FileType markdown match none
  autocmd FileType mkd      match none
  autocmd FileType php      match none
  autocmd FileType php      match none
  autocmd FileType plaintex match none
  autocmd FileType qf       match none
  autocmd FileType sql      match none
  autocmd FileType tex      match none
  autocmd FileType text     match none
  autocmd FileType unite    match none
  autocmd FileType vim      match none
  autocmd FileType vimfiler match none
  autocmd FileType xhtml    match none
  autocmd FileType xml      match none
augroup END

augroup configureFolds
  autocmd!
  autocmd FileType mkd      setlocal spell nofoldenable
  autocmd FileType markdown setlocal spell nofoldenable
  autocmd FileType text     setlocal spell nofoldenable
  autocmd FileType vim      setlocal foldmethod=marker
augroup END

augroup resumeCursorPosition
  autocmd!
  autocmd BufReadPost *
    \ if line("'\"") > 0 && line("'\"") <= line("$") |
    \   exe "normal g`\"" |
    \ endif
augroup END

augroup FixProportionsOnResize
  au!
  au VimResized * exe "normal! \<c-w>="
augroup END

augroup miscGroup
  autocmd!
  " set comments for SML
  autocmd FileType sml set commentstring=(*\ %s\ *)

  " jump the top in git commit messages
  autocmd FileType gitcommit normal gg

  " somehow this is required to move the gray color of the sign column
  autocmd FileType * highlight clear SignColumn

  " when in a git commit buffer go the beginning
  autocmd FileType gitcommit au! BufEnter COMMIT_EDITMSG call setpos('.', [0, 1, 1, 0])

  " save files when focus is lost
  autocmd BufLeave * silent! update

  " configure indentation for python
  autocmd FileType python set expandtab tabstop=4 softtabstop=4 shiftwidth=4

  " Disable spell checking in vim help files
  autocmd FileType help set nospell
augroup END

" ========================================
" == Mappings ============================
" ========================================

" Rather than having loads of comments above my mappings I
" try to make well named functions
source ~/.vim/functions.vim

" Disable useless and annoying keys
noremap Q <Nop>
noremap K <Nop>

" Don't wanna retrain my fingers
command! W w
command! Q q
command! Qall qall

" Make Y work as expected
nnoremap Y y$

" Intuitive movement over long lines
nnoremap k gk
nnoremap j gj

nnoremap gjb :call JsBindFunction()<cr>
nnoremap gjt :call JsToggleFunctionStatement()<cr>

" Resize windows with the arrow keys
nnoremap <up>    <C-W>+
nnoremap <down>  <C-W>-
nnoremap <left>  3<C-W>>
nnoremap <right> 3<C-W><

" Move text around in visual mode
vnoremap <left>  <nop>
vnoremap <right> <nop>
vnoremap <up> xkP`[V`]
vnoremap <down> xp`[V`]

" Spell correct current word
imap <c-z> <esc><leader>zea

imap <c-s> <esc>:w<cr>
map <c-s> <esc>:w<cr>

" insert current file name with \f in insert mode
" inoremap \f <C-R>=expand("%:t:r")<CR>

" insert that to current file
cnoremap %% <C-R>=expand('%:h').'/'<cr>

" sort the selected lines
vnoremap gs :sort<cr>

command! FindConditionals :normal /\<if\>\|\<unless\>\|\<and\>\|\<or\>\|||\|&&<cr>

command! RemoveFancyCharacters :call RemoveFancyCharacters()

" ========================================
" == Leader mappings =====================
" ========================================

let mapleader = "\<Space>"

vnoremap <leader>. :normal .<cr>

noremap <leader><leader> <C-^>

noremap <leader>; maA;<esc>`a
noremap <leader>== magg=G`a
vnoremap <leader>= :Tabularize /
noremap <leader>/ /\v
noremap <leader>? ?\v

"-- a --"
noremap <leader>a :call YankWholeBuffer(0)<cr>
noremap <leader>A :call YankWholeBuffer(1)<cr>

"-- b --"

"-- c --"
noremap <leader>cd :cd %:p:h<cr>:pwd<cr>

"-- d --"
noremap <leader>do :call ToggleRubyBlockSyntax()<cr>
noremap <leader>di :Dispatch<space>

"-- e --"
noremap <leader>ev :tabedit $MYVIMRC<cr>
noremap <leader>es :UltiSnipsEdit<cr>

"-- f --"
nnoremap <leader>F :CommandTFlush<cr>:CommandT<cr>
nnoremap <leader>f :CommandT<cr>

"-- g --"
noremap <leader>g :Git<space>
noremap <leader>gap :Git add -p<cr>
noremap <leader>gb :Gblame<cr>
noremap <leader>gc :Gcommit<cr>
noremap <leader>gd :Gdiff<cr>
noremap <leader>gp :Git push<cr><cr>
noremap <leader>gr :Gremove<cr>
noremap <leader>gs :Gstatus<cr>
noremap <leader>ga :Gwrite<cr>
noremap <leader>gg :w<cr>:Gwrite<cr>:Gcommit -m 'update'<cr>:Git push<cr><cr>:e<cr>
noremap <leader>gu :GundoToggle<cr>

"-- h --"

"-- i --"

"-- j --"

"-- k --"
noremap <leader>k :w\|:call RunNearestSpec()<CR>

"-- l --"
noremap <leader>l :call MakeList()<cr>

"-- m --"
noremap <leader>mh :call MakeMarkdownHeading(1)<cr>
noremap <leader>mH :call MakeMarkdownHeading(2)<cr>
vnoremap <leader>mlc :call FormatSmlComments()<cr>

"-- n --"

"-- o --"
noremap <leader>o :only<cr>
noremap <leader>O :!open %<cr><cr>

"-- p --"
noremap <leader>p :call PasteFromSystemClipBoard()<cr>
noremap <leader>pi :PluginInstall<cr>
noremap <leader>pc :PluginClean<cr>
noremap <leader>pu :PluginUpdate<cr>

"-- q --"
noremap <leader>q :call CloseExtraPane()<cr>

"-- r --"
noremap <leader>rr :w\|:call RunCurrentFile()<cr>
noremap <leader>rbi :w\|:Dispatch bundle install<cr>
noremap <leader>rd :redraw!<cr>
noremap <leader>re :call FixFormatting()<cr>
noremap <leader>rn :call RenameFile()<cr>

noremap <leader>ra :Dispatch bin/rails 
noremap <leader>rk :Dispatch bin/rake 

nnoremap <leader>rel :call PromoteToLet()<cr>

"-- s --"
noremap <leader>ss :vsp<cr>:A<cr>
noremap <leader>sv :source $MYVIMRC<cr>

"-- t --"
map <leader>t :w<cr>:call spectacular#run_tests()<cr>
map <leader>T :w<cr>:Dispatch rspec<cr>

"-- u --"

"-- v --"
noremap <leader>v :vsplit<cr>

"-- w --"
noremap <leader>W :wq<cr>

"-- x --"
noremap <leader>x :set filetype=

"-- y --"
noremap <leader>y "+y

"-- z --"
noremap <leader>z :call CorrectSpelling()<cr>

" ========================================
" == Command-T config ====================
" ========================================

let g:CommandTWildIgnore=&wildignore
      \.",**/bower_components/*"
      \.",**/tmp/*"
      \.",**/*.png"
      \.",**/*.jpg"
      \.",**/*.jpeg"
      \.",**/*.gif"
      \.",**/*.eot"
      \.",**/*.woff"
      \.",**/*.ttf"
      \.",**/*.pdf"
      \.",**/*.aux"
      \.",**/*.log"
      \.",**/*.class"
      \.",**/*.gz"
      \.",**/*.psd"
      \.",**/node_modules/*"
      \.",**/vendor/*"
      \.",**/_site/*"
      \.",**/dist/*"
      \.",**/vim.symlink/bundle/*"
      \.",**/vendor/assets/components/*"
      \.",*.alfredpreferences"
      \.",*.pyc"

let g:CommandTCancelMap='<C-[>'

" ========================================
" == Misc plugin config ==================
" ========================================

let g:UltiSnipsEditSplit = 'horizontal'
let g:UltiSnipsSnippetDirectories = ["ultisnips"]

let g:multi_cursor_exit_from_visual_mode = 0

let g:ruby_refactoring_map_keys = 0

let NERDTreeIgnore = ['\.class$']

let g:spectacular_integrate_with_tmux = 0
let g:spectacular_integrate_with_dispatch = 1
let g:spectacular_debugging_mode = 0

let g:switch_custom_definitions =
  \ [
  \   ['foo', 'bar', 'baz', 'qux'],
  \   ['white', 'black'],
  \   ['margin', 'padding'],
  \   ['left', 'right'],
  \   ['top', 'bottom'],
  \   ['addClass', 'removeClass'],
  \   ['fast', 'slow'],
  \   ['public', 'private', 'protected'],
  \   ['height', 'width']
  \ ]

let g:rspec_command = "Dispatch rspec {spec}"

" ========================================
" == Test running ========================
" ========================================

call spectacular#add_test_runner('cucumber', './vendor/bin/behat {spec}', '', function("InPhpProject"))
call spectacular#add_test_runner('cucumber', 'bin/cucumber {spec} -t @focus', '', function("WithCucumberFocusTag"), function("InRailsApp"))
call spectacular#add_test_runner('cucumber', 'cucumber {spec} -t @focus', '', function("WithCucumberFocusTag"))
call spectacular#add_test_runner('cucumber', 'bin/cucumber {spec}', '', function("InRailsApp"))
call spectacular#add_test_runner('cucumber', 'cucumber {spec}', '')
call spectacular#add_test_runner('ruby', 'bin/cucumber', '_steps', function("InRailsApp"))
call spectacular#add_test_runner('ruby', 'cucumber', '_steps')
call spectacular#add_test_runner('ruby, javascript, eruby, coffee, haml', 'bin/rspec --tag focus:true {spec}', '_spec.rb', function("TestsInRails"), function("HasRspecFocusTag"))
call spectacular#add_test_runner('ruby, javascript, eruby, coffee, haml', 'bin/rspec {spec}', '_spec.rb', function("TestsInRails"))
call spectacular#add_test_runner('ruby, javascript, eruby, coffee, haml', 'bundle exec --tag focus:true rspec {spec}', '_spec,rb', function("IncludesRspecGem"), function("HasRspecFocusTag"))
call spectacular#add_test_runner('ruby, javascript, eruby, coffee, haml', 'bundle exec rspec {spec}', '_spec,rb', function("IncludesRspecGem"))
call spectacular#add_test_runner('ruby, javascript, eruby, coffee, haml', 'rspec {spec}', '_spec.rb')
call spectacular#add_test_runner('python', 'nosetests --rednose', 'test_')
call spectacular#add_test_runner('php', 'vendor/bin/phpunit --colors {spec}', '_tests')
call spectacular#add_test_runner('sml', 'smlspec {spec}', '')
call spectacular#add_test_runner('javascript', 'mocha {spec}', '_spec')
call spectacular#add_test_runner('javascript', 'mocha {spec}', 'Spec')
call spectacular#add_test_runner('coffee', 'mocha --compilers coffee:coffee-script/register {spec}', '_spec')
call spectacular#add_test_runner('coffee', 'mocha --compilers coffee:coffee-script/register {spec}', 'Spec')
call spectacular#add_test_runner('java', 'javac *.java && junit {spec}', 'Test')
call spectacular#add_test_runner('elixir', 'mix test {spec}', '_test')
call spectacular#add_test_runner('clojure', 'lein test {spec}', '_test')

" ========================================
" == Local config ========================
" ========================================

try
  source ~/.vimrc.local
catch
endtry
